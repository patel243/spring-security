buildscript {
	repositories {
		maven { url 'https://repo.spring.io/plugins-release' }
	}
	dependencies {
		classpath 'io.spring.gradle:propdeps-plugin:0.0.10.RELEASE'
	}
}

plugins {
	id 'java'
	id 'java-library'
	id 'io.spring.convention.repository'
	id 'io.spring.convention.management-configuration'
	id 'io.spring.convention.dependency-set'
	id 'io.spring.convention.checkstyle'
	id 'io.spring.convention.tests-configuration'
	id 'io.spring.convention.integration-test'
	id 'propdeps'
}

configurations {
	classesOnlyElements {
		canBeConsumed = true
		canBeResolved = false
	}
	sourceElements {
		canBeConsumed = true
		canBeResolved = false
	}
	javadocElements {
		canBeConsumed = true
		canBeResolved = false
	}
}

artifacts {
	classesOnlyElements(compileJava.destinationDir)
	sourceSets.main.allSource.srcDirs.forEach({ dir ->
		sourceElements(dir)
	})
	javadocElements(javadoc.destinationDir)
}

repositories {
	maven { url "https://build.shibboleth.net/nexus/content/repositories/releases/" }
}

dependencies {
	management platform(project(":spring-security-dependencies"))

	api project(':spring-security-core')
	api project(':spring-security-web')

	provided("org.opensaml:opensaml-core")
	provided("org.opensaml:opensaml-saml-api")
	provided("org.opensaml:opensaml-saml-impl")

	provided 'javax.servlet:javax.servlet-api'

	testImplementation 'com.squareup.okhttp3:mockwebserver'
}
